module generate/common

imports
	
	include/Xtext
	lib/TemplateLang

rules 

	id-to-name:
		ID -> Unparameterized(name)
		where
			name := <last> ID
	
	remove-sortdef:
		SortDef(name) -> name
 
  unordered(s):
    (x, y) -> <<s> (x,y) + <s> (y,x)>
  
rules // Cardinality
	
	gen-cardinality(|abstract-terminal):
		None() -> abstract-terminal
	
	gen-cardinality(|abstract-terminal):
		Some(Any()) -> IterStar(abstract-terminal)
	
	gen-cardinality(|abstract-terminal):
		Some(Multiple()) -> Iter(abstract-terminal)
		
	gen-cardinality(|abstract-terminal):
		Some(Optional()) -> Opt(abstract-terminal)
	
rules // Permutations (new)
  
	permutations:
		[x] -> [[x]]
	
	permutations:
		xs -> <map(permutations(|xs)) ; concat> xs
	
	permutations(|xs):
		x -> <filter(not(?x)) ; permutations ; map(prepend(|x))> xs
	
	prepend-list:
		(x, xss) -> <map(prepend(|x))> xss
	
	prepend(|x) = ?t ; ![x | t]
	
rules // Permutations (old)
	
	gen-permutations(|perm-set):
		_ -> permutations
		where
			permutations := <recursive-permutations(|perm-set, perm-set, <length> perm-set)>
			
	recursive-permutations(|set, block, n):
		_ -> <if(<eq> (1,n), <filter-permutations(|block)> set, recursive-permutations(|new_set, block, <dec> n))>
		where
			new_set := <list-combinations> [set, block]
			
	filter-permutations(|block):
		set -> filtered-permutations
		where
			filtered-list := <collect-all(filter(|block))> set;
			filtered-permutations := <map(TupleToList; last)> filtered-list
	
	filter(|block):
		set -> <set-eq> (block, set)

rules // List utils
  
  /**
   * Append an element to the end of a list
   * 
   * @type (List(a), a) -> List(a)
   */
  append:
    (list, e) -> <reverse> [e | <reverse> list]
    
  is-empty = ?[]

  list-to-conc:
    [singleton] -> singleton
  
  list-to-conc:
    [head | tail] -> Conc(head, <list-to-conc> tail)
  
  alt-to-list = ?Alt(x, y); ![x | <alt-to-list> y] <+ ![<id>]
  
rules // Rewrite reserved words
  
  sort-name = string-as-chars(map(try(?'_'; !'-')))
  
rules // Pattern match left- and right associative ParserRules
  
  is-left-assoc = is-simple-left-assoc + is-nested-left-assoc
  
  is-simple-left-assoc:
    ParserRule(name, _, _, Alternatives([UnorderedGroup([Group(tokens)])])) -> <true>
  where
    <is-left-assoc-tokens> tokens
  
  is-nested-left-assoc:
    ParserRule(name, _, _, Alternatives([
      UnorderedGroup([
        Group([
          AbstractTerminalAbstractToken(
            Alternatives([
              UnorderedGroup([
                Group(tokens)
              ])
            ]),
            Some(Optional())
          )
        ])
      ])
    ])) -> <true>
  where
    <is-left-assoc-tokens> tokens
  
  is-left-assoc-tokens:
    tokens -> <true>
  where
    // "<first> tokens" is a RuleCall with a name
    ?[AbstractTerminalAbstractToken(RuleCall(name), None()) | _];
    
    // "<last> tokens > group" has cardinality any
    <last> tokens; ?AbstractTerminalAbstractToken(Alternatives([UnorderedGroup([Group(sub-tokens)])]), Some(Any()));

    // "<last> tokens > group" starts with an action which assigns 'current'
    !sub-tokens; ?[ActionAbstractToken(Action(TypeRef(None(), _), Some(ActionCurrent(_, Assignment())))) | _];
    
    // "<last> tokens > group" ends with an assignment that calls a rule with the same name as above
    <last> sub-tokens; ?AssignmentAbstractToken(None(), _, Assignment(), RuleCall(name), None())
  
  is-right-assoc:
    ParserRule(name, _, _, Alternatives([UnorderedGroup([Group(tokens)])])) -> <true>
  where
    // "<last> tokens" has cardinality Optional
    <last> tokens; ?AbstractTerminalAbstractToken(Alternatives([UnorderedGroup([Group(sub-tokens)])]), Some(Optional()));
    
    // "<last> tokens > group" starts with action with same name as rule
    !sub-tokens; ?[h | _]; !h; ?ActionAbstractToken(Action(TypeRef(None(), name), Some(ActionCurrent(_, Assignment()))));
    
    // "<last> tokens > group" ends with assignment with rulecall to same as name
    <last> sub-tokens; ?AssignmentAbstractToken(None(), _, Assignment(), RuleCall(name), None())
    
   		
	is-empty = ?[]

  list-to-conc:
    [singleton] -> singleton
  
  list-to-conc:
    [head | tail] -> Conc(head, <list-to-conc> tail)

rules // Files
  
  /**
   * Write content to path
   * 
   * @type (String, String) -> true
   */
  write-text-file =
    ?(path, content)
  ; fd := <fopen> (path, "w")
  ; <fputs> (content, fd)
  ; <fclose> fd
