module terminal-rule

imports
	
	include/Xtext
	generate/common

rules
	
	gen-rule:
		TerminalRule(Returns(terminal-name, _), TerminalAlternatives(terminal-alternatives)) -> $[
			lexical syntax
		    	[terminal-name] = [<map(gen-terminal-alternative) ; flatten-list ; separate-by(|" | ")> terminal-alternatives]
		]
	
	gen-terminal-alternative:
		TerminalGroup(terminal-group) -> $<[<gen-terminal-group>]>
		where
			gen-terminal-group := <map(gen-terminal-group) ; concat-strings> terminal-group
	
	gen-terminal-group:
		TerminalToken(terminal-token-element, cardinality-opt) -> $[[<gen-terminal-token-element> terminal-token-element][<gen-cardinality-opt> cardinality-opt]]
	
	gen-terminal-token-element:
		TerminalAlternatives(terminal-alternatives) -> $<[<gen-terminal-alternative>]>
		where
			gen-terminal-alternative := <map(gen-terminal-alternative) ; flatten-list ; concat-strings> terminal-alternatives
	
	gen-terminal-token-element:
		Keyword(s) -> s
	
	gen-terminal-token-element:
		CharacterRange(x,y) -> $[[x]-[y]]
		
	gen-terminal-token-element:
		RuleCall(reference) -> <fail>
		where
			<debug> "TODO"
	
	gen-terminal-token-element:
		NegatedToken(terminal-token-element) -> $<~[<gen-terminal-token-element>]>
		where
			gen-terminal-token-element := <gen-terminal-token-element> terminal-token-element